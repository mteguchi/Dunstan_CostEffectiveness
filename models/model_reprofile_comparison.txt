# This JAGS model compares the mean number of hatchlings per sector per year
# between modified and control sectors. There are three modified sectors:
# 2014R, 2017R, and 2019R, which are compared to the South sector (control).

# Tomo Eguchi
# 2021-10-28
# 2022-04-23 Updated for new data from 2022 season

model{
	
	for (i in 1:n.South.seasons){
		for (nSouth in 1:NSouth[i]){
			ySouth[nSouth, i] ~ dnorm(muSouth[i], tauSouth[i])
		}

		muSouth[i] ~ dnorm(0, 0.1)I(0,)
		sigmaSouth[i] ~ dgamma(0.1, 0.1)
		tauSouth[i] <- 1/((sigmaSouth[i])^2)
		
	}

	# 2014 and South are the same length
	for (i in 1:n.2014.seasons){
		# 2014R
		for (n2014 in 1:N2014[i]){
			y2014[n2014, i] ~ dnorm(mu2014[i], tau2014[i])
		}

		mu2014[i] ~ dnorm(0, 0.1)I(0,)
		sigma2014[i] ~ dgamma(0.1, 0.1)
		tau2014[i] <- 1/((sigma2014[i])^2)

		deltaMu2014[i] <- exp(mu2014[i]) - exp(muSouth[i])
		ratio2014[i] <- exp(mu2014[i])/exp(muSouth[i])

	}
	

	for (j in 1:n.2017.seasons){
		# 2017R
		for (n2017 in 1:N2017[j]){
			y2017[n2017, j] ~ dnorm(mu2017[j], tau2017[j])
		}

		mu2017[j] ~ dnorm(0, 0.1)I(0,)
		sigma2017[j] ~ dgamma(0.1, 0.1)
		tau2017[j] <- 1/((sigma2017[j])^2)

	    # need to offset by 3 years
		deltaMu2017[j] <- exp(mu2017[j]) - exp(muSouth[j+3])
		ratio2017[j] <- exp(mu2017[j])/exp(muSouth[j+3])

	}	

	for (k in 1:n.2019.seasons){
		# 2019R
		for (n2019 in 1:N2019[k]){
			y2019[n2019, k] ~ dnorm(mu2019[k], tau2019[k])
		}

		mu2019[k] ~ dnorm(0, 0.1)I(0,)
		sigma2019[k] ~ dgamma(0.1, 0.1)
		tau2019[k] <- 1/((sigma2019[k])^2)

	    # need to offset by 5 years.
		deltaMu2019[k] <- exp(mu2019[k]) - exp(muSouth[k+5])
		ratio2019[k] <- exp(mu2019[k])/exp(muSouth[k+5])
	}	

}