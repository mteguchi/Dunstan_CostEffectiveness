# JAGS model for estimating hatchling counts at Raine Island

# Model to estimate hatchling counts from 2400 to 0600 using covariates.
# This is part of exercise to determine if the 6-hr sampling is sufficient
# to determine the hatchling production at Raine Island. Hatchling 
# counts were conducted for 12 hrs in the past but due to the logistical
# challenges, they reduced the sampling to 6 hrs in the recent years.
# I estimate the second half of observed hatchling counts from the first 
# half and older data, i.e., 12-hr sampling.

# Details can be found in hatchling_counts_sampling_comparison.Rmd

# 2022-11-29 Tomo Eguchi
 

model{

	for (k in 1:n.data){
		n[k] ~ dnorm(lambda[year[k], sector[k], seq_date[k]], tau.n)
		m[k] ~ dnorm(lambda[year[k], sector[k], seq_date[k]] * p[year[k]], tau.n)
		
		loglik.n[k] <- logdensity.norm(n[k], lambda[year[k], sector[k], seq_date[k]], tau.n) 
		loglik.m[k] <- logdensity.norm(m[k], lambda[year[k], sector[k], seq_date[k]] * p[year[k]], tau.n)  
		
		lambda[year[k], sector[k], seq_date[k]] <- length[k] * exp(S[sector[k]] + B.day * day[k] + B.N.fem * N.fem[k])
	
	}
	
	for (k in 1:n.sectors){
		S[k] ~ dnorm(mu.S, tau.S)
	}
	
	for (y in 1:n.years){
		p[y] ~ dunif(0, 2) #dbeta(2,2)
    }
	
	mu.S ~ dnorm(0, 0.01)
	tau.S <- 1/var.S
	var.S ~ dgamma(1, 0.1)
	sigma.S <- sqrt(var.S)
	
	tau.n <- 1/var.n
	var.n ~ dgamma(1, 0.1)
	sigma.n <- sqrt(var.n)
	
	B.N.fem ~ dnorm(0, 0.01)
	B.day ~ dnorm(0, 0.01)

}
