# JAGS model for estimating hatchling counts at Raine Island

# Model to estimate hatchling counts from 2400 to 0600 using covariates.
# This is part of exercise to determine if the 6-hr sampling is sufficient
# to determine the hatchling production at Raine Island. Hatchling 
# counts were conducted for 12 hrs in the past but due to the logistical
# challenges, they reduced the sampling to 6 hrs in the recent years.
# I estimate the second half of observed hatchling counts from the first 
# half and older data, i.e., 12-hr sampling.

# Details can be found in hatchling_counts_sampling_comparison.Rmd

# For v2, I changed p to be year and sector specific. 

# 2022-11-29 Tomo Eguchi
 

model{
	for (y in 1:n.years){
		for (j in 1:n.sectors[y]){
			for (k in 1:n.days[y,j]){
				n[y,sectors[y,j], k] ~ dpois(lambda[y,sectors[y,j],k])
				m[y,sectors[y,j], k] ~ dpois(lambda[y,sectors[y,j],k] * p[y,j])
				
				# + Y[years[y]] removed from below because it did not converge
				lambda[y,sectors[y,j],k] <- length[y,sectors[y,j],k] * exp(S[sectors[y,j]] + B.N.fem * N.fem[y] + B.day * D[y,sectors[y,j],k])
								
			}
		}
		
	}
	
	for (k in 1:num.sectors){
		S[k] ~ dnorm(mu.S, tau.S)
		#Y[k] ~ dnorm(mu.Y, tau.Y)
	}
	
	mu.S ~ dnorm(0, 0.01)
	tau.S <- 1/var.S
	var.S ~ dgamma(0.1, 0.01)
	sigma.S <- sqrt(var.S)
	
	mu.Y ~ dnorm(0, 0.01)
	tau.Y <- 1/var.Y
	var.Y ~ dgamma(0.1, 0.01)
	sigma.Y <- sqrt(var.Y)

	#B0 ~ dnorm(0, 0.01)
	#B1 ~ dnorm(0, 0.01)
	B.N.fem ~ dnorm(0, 0.01)
	B.day ~ dnorm(0, 0.01)

	for (k in 1:n.years){
		for (j in 1:n.sectors[k]){
			p[k,j] ~ dbeta(2, 2)
		}

	}
}
